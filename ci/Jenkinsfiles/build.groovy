/*
* (C) Copyright 2023-2025 Nuxeo (http://nuxeo.com/) and others.
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*     http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*
* Contributors:
*     Kevin Leturc <kevin.leturc@hyland.com>
*/
library identifier: "platform-ci-shared-library@v0.0.75"

String getWebUIVersion() {
  container('maven') {
    dir('nuxeo-retention-web') {
      return sh(
        returnStdout: true,
        script: '''
          jq --raw-output '.packages."node_modules/@nuxeo/nuxeo-web-ui-ftest".version' package-lock.json
        '''
      ).trim()
    }
  }
}

Closure buildUnitTestStage(env) {
  return {
    container('maven') {
      nxWithGitHubStatus(context: "utests/backend/${env}") {
        script {
          def testNamespace = "${CURRENT_NAMESPACE}-retention-${BRANCH_NAME}-${BUILD_NUMBER}-${env}".replaceAll('\\.', '-').toLowerCase()
          nxWithHelmfileDeployment(namespace: testNamespace, environment: "${env}UnitTests", cacheName: env) {
            try {
              sh """
                cat ci/mvn/nuxeo-test-${env}.properties \
                  ci/mvn/nuxeo-test-kafka.properties \
                | envsubst > /root/nuxeo-test-${env}.properties
              """
              retry(3) {
                sh """
                  mvn ${MAVEN_CLI_ARGS} -pl :nuxeo-retention \
                    -Dcustom.environment=${env} \
                    test
                """
              }
            } finally {
              archiveArtifacts artifacts: "**/target-${env}/**/*.log"
              junit allowEmptyResults: true, testResults: "**/target-${env}/surefire-reports/*.xml"
            }
          }
        }
      }
    }
  }
}

pipeline {
  agent {
    label 'jenkins-nuxeo-package-lts-2025'
  }
  options {
    buildDiscarder(logRotator(daysToKeepStr: '60', numToKeepStr: '60', artifactNumToKeepStr: '5'))
    disableConcurrentBuilds(abortPrevious: true)
    githubProjectProperty(projectUrlStr: 'https://github.com/nuxeo/nuxeo-retention')
  }
  environment {
    CURRENT_NAMESPACE = nxK8s.getCurrentNamespace()
    TEST_SERVICE_DOMAIN_SUFFIX = 'svc.cluster.local'
    MAVEN_OPTS = "$MAVEN_OPTS -Xms512m -Xmx3072m"
    MAVEN_CLI_ARGS = "-B -V -nsu -Dnuxeo.skip.enforcer=true -Prelease"
    NUXEO_VERSION = nxMvn.getProperty(key: 'project.parent.version')
    VERSION = nxUtils.getVersion()
    NUXEO_RETENTION_PACKAGE_PATH = "nuxeo-retention-package/target/nuxeo-retention-package-${VERSION}.zip"
    NUXEO_WEB_UI_VERSION = getWebUIVersion()
  }
  stages {
    stage('Set labels') {
      steps {
        container('maven') {
          script {
            nxK8s.setPodLabels()
          }
        }
      }
    }
    stage('Update version') {
      steps {
        container('maven') {
          script {
            nxMvn.updateVersion()
          }
        }
      }
    }
    stage('Build') {
      parallel {
        stage('Compile') {
          steps {
            container('maven') {
              nxWithGitHubStatus(context: 'compile') {
                echo """
                ----------------------------------------
                Compile
                ----------------------------------------"""
                echo "MAVEN_OPTS=$MAVEN_OPTS"
                sh """
                  mvn ${MAVEN_CLI_ARGS} -T4C install -DskipTests \
                    -Dfrontend-plugin.node.server.id=nexus-internal \
                    -Dfrontend-plugin.node.download.root=https://${NODE_DIST_REGISTRY} \
                    -Dfrontend-plugin.node.npm.userconfig=${NPM_CONFIG_USERCONFIG}
                """
              }
            }
          }
          post {
            always {
              archiveArtifacts artifacts: '**/target/*.jar, **/target/nuxeo-*-package-*.zip'
              junit testResults: '**/target/surefire-reports/*.xml, **/target/failsafe-reports/*.xml', allowEmptyResults: true
            }
          }
        }
        stage('Formatting check') {
          when {
            // if current version is higher than default branch (aka: version in maintenance) run formatting check
            expression { nxGitHub.getReferenceBranch().compareToIgnoreCase(nxGitHub.getDefaultBranch()) > 0 }
          }
          steps {
            container('maven') {
              warnError(message: 'Formatting check has failed') {
                nxWithGitHubStatus(context: 'maven/lint', message: 'Lint') {
                  script {
                    echo """
                    ----------------------------------------
                    Check formatting
                    ----------------------------------------"""
                    sh "git fetch origin lts-2025:origin/lts-2025"
                    sh "mvn ${MAVEN_CLI_ARGS} -V -Dcustom.environment=spotless spotless:check"
                  }
                }
              }
            }
          }
        }
        stage('Enforcer check') {
          steps {
            container('maven') {
              warnError(message: 'Enforcer check has failed') {
                nxWithGitHubStatus(context: 'maven/enforcer', message: 'Enforce') {
                  script {
                    echo """
                    ----------------------------------------
                    Check enforcer rules
                    ----------------------------------------""".stripIndent()
                    sh "mvn ${MAVEN_CLI_ARGS} -Dcustom.environment=enforcer enforcer:enforce"
                  }
                }
              }
            }
          }
        }
      }
    }
    stage('Run unit tests') {
      steps {
        script {
          def stages = [:]
          stages['Frontend'] = {
            container('playwright') {
              nxWithGitHubStatus(context: 'utests/frontend') {
                dir('nuxeo-retention-web') {
                  sh 'npm install'
                  sh 'npm install --no-save playwright'
                  sh 'npx playwright install --with-deps'
                  sh 'npm run test'
                }
              }
            }
          }
          stages['Backend - dev'] = {
            container('maven') {
              nxWithGitHubStatus(context: 'utests/backend/dev') {
                try {
                  // empty file required by the read-project-properties goal of the properties-maven-plugin with the
                  // customEnvironment profile
                  sh 'touch /root/nuxeo-test-dev.properties'
                  retry(3) {
                    sh "mvn ${MAVEN_CLI_ARGS} -pl :nuxeo-retention -Dcustom.environment=dev test"
                  }
                } finally {
                  archiveArtifacts artifacts: '**/target-dev/**/*.log'
                  junit allowEmptyResults: true, testResults: "**/target-dev/surefire-reports/*.xml"
                }
              }
            }
          }
          stages['Backend - MongoDB'] = buildUnitTestStage('mongodb')
          stages['Backend - PostgreSQL'] = buildUnitTestStage('postgresql')
          parallel stages
        }
      }
    }
    stage('Run functional tests') {
      steps {
        container('maven') {
          script {
            // target connect preprod if nuxeo-parent is a snapshot version or a build version
            def clidSecret = env.NUXEO_VERSION.matches("^\\d+\\.\\d+(-SNAPSHOT|\\.\\d+)\$") ? 'instance-clid-preprod' : 'instance-clid'
            def clid = nxK8s.getSecretData(namespace: 'platform', name: clidSecret, key: 'instance\\.clid')
            def connectUrl = clidSecret.contains('preprod') ? CONNECT_PREPROD_SITE_URL : CONNECT_PROD_SITE_URL
            def nuxeoWebUIPackage = "nuxeo-web-ui-${NUXEO_WEB_UI_VERSION}"

            nxWithGitHubStatus(context: 'docker/build') {
              sh "mkdir -p ci/docker/target && cp ${NUXEO_RETENTION_PACKAGE_PATH} ci/docker/target"
              def nuxeoVersion = nxMvn.getProperty(key: 'nuxeo.platform.version')
              // use withEnv for clid to not print it to the console, which nxDocker does
              withEnv(["CLID=${clid}"]) {
                nxDocker.build(
                  skaffoldFile: 'ci/docker/skaffold.yaml',
                  envVars: [
                    "CONNECT_URL=${connectUrl}",
                    "NUXEO_VERSION=${nuxeoVersion}",
                    "NUXEO_WEB_UI_PACKAGE=${nuxeoWebUIPackage}"
                  ]
                )
              }
            }
            nxWithGitHubStatus(context: 'ftests') {
              def testNamespace = "${CURRENT_NAMESPACE}-retention-${BRANCH_NAME}-${BUILD_NUMBER}-ftests".replaceAll('\\.', '-').toLowerCase()
              nxWithHelmfileDeployment(namespace: testNamespace, environment: "functionalTests", envVars: ["CONNECT_CLID_SECRET=${clidSecret}"],
                  secrets: [[name: clidSecret, namespace: 'platform']]) {
                dir('nuxeo-retention-web') {
                  sh """
                      mvn ${MAVEN_CLI_ARGS} com.github.eirslett:frontend-maven-plugin:npm@ftest -Pftest \
                      -Dfrontend-plugin.ftest.nuxeoUrl=http://nuxeo.${NAMESPACE}.svc.cluster.local/nuxeo
                    """
                }
              }
            }
          }
        }
      }
      post {
        always {
          archiveArtifacts artifacts: 'nuxeo-retention-web/ftest/target/screenshots/**', allowEmptyArchive: true
          cucumber(fileIncludePattern: '**/*.json', jsonReportDirectory: 'nuxeo-retention-web/ftest/target/cucumber-reports/',
              sortingMethod: 'NATURAL')
        }
      }
    }
    stage('Git commit, tag and push') {
      when {
        expression { !nxUtils.isPullRequest() }
      }
      steps {
        container('maven') {
          script {
            echo """
            ----------------------------------------
            Git commit, tag and push
            ----------------------------------------
            """
            nxGit.commitTagPush()
          }
        }
      }
    }
    stage('Deploy Maven artifacts') {
      when {
        expression { !nxUtils.isPullRequest() }
      }
      steps {
        container('maven') {
          nxWithGitHubStatus(context: 'maven/deploy', message: 'Deploy Maven artifacts') {
            script {
              echo """
              ----------------------------------------
              Deploy Maven artifacts
              ----------------------------------------"""
              nxMvn.deploy()
            }
          }
        }
      }
    }
    stage('Deploy Nuxeo package') {
      when {
        expression { !nxUtils.isPullRequest() }
      }
      steps {
        container('maven') {
          nxWithGitHubStatus(context: 'package/deploy', message: 'Deploy Nuxeo packages') {
            script {
              echo """
              ----------------------------------------
              Upload Nuxeo Package to ${CONNECT_PREPROD_SITE_URL}
              ----------------------------------------"""
              nxUtils.postForm(credentialsId: 'connect-preprod', url: "${CONNECT_PREPROD_SITE_URL}marketplace/upload?batch=true",
                  form: ["package=@${NUXEO_RETENTION_PACKAGE_PATH}"])
            }
          }
        }
      }
    }
  }
  post {
    always {
      script {
        nxUtils.setBuildDescription()
        nxJira.updateIssues()
        nxUtils.notifyBuildStatusIfNecessary()
      }
    }
  }
}

